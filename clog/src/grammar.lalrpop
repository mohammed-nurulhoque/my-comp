use crate::{
    ast::*,
    types::{ProtoType, Literal, BinOpcode, UnOpcode}
};

grammar<'err>(errors: &'err mut Vec<usize>);

pub Program: Vec<Binding<'input>> = {
    Statement*,
};

Statement: Binding<'input> = {
    TypeDecl,
    ValBinding,
    FnBinding,
}

Comma<T> : Vec<T> = {
    <T> => vec![<>],
    <v: (<T> ",")+> <last: T?> => {
        let mut v = v;
        if let Some(e) = last {
            v.push(e);
        }
        v
    }
}

// at least 2 elements
Comma2<T>: Vec<T> = {
    <v: (<T> ",")+> <last: T> ","? => {
        let mut v = v;
        v.push(last);
        v
    }
}

TypeDecl: Binding<'input> = {
    "type" <name:ID> <vars:TypeVars?> "=" <variants:SumVarDecl+> => {
        Binding::Type { name, vars: vars.unwrap_or(vec![]), variants }
    }
};

TypeVars: Vec<&'input str> = {
    "()" => vec![],
    ID => vec![<>],
    "(" <Comma<ID>> ")",
}

SumVarDecl: (&'input str, ProtoType<'input>) = {
    "|" <ID> <Type>,
};

ValBinding: Binding<'input> = {
    "let" <Pattern> "=" <Expr> => Binding::Value(<>, false),
};

FnBinding: Binding<'input> = {
    "rec" <name:ID> "=" "{" <v:Comma<Arm>> "}" => Binding::Value(Pattern::Bind(name), Expr::Closure(v), true),
};

Arm = {
    <Pattern+> "=>" <Expr>,
};

Pattern: Pattern<'input> = {
    Literal => Pattern::Literal(<>),
    "_" => Pattern::Wild,
    ID => Pattern::Bind(<>),
    "(" <v:Comma2<PatternH>> ")" => Pattern::Tuple(v),
    "(" <PatternH> ")",
    <start: @L> ! <end: @R> => { errors.push(start); Pattern::Error(<>) },
};

PatternH: Pattern<'input> = {
    <id:ID> <field:Pattern> =>  Pattern::SumVar(id, Box::new(field)),
    Pattern,
};

Class<Op,NextClass>: Expr<'input> = {
    <l:Class<Op,NextClass>> <op:Op> <r:NextClass> => 
        Expr::BinOp(Box::new(l), op, Box::new(r)),
    NextClass,
};

UnaryClass<Op, NextClass>: Expr<'input> = {
    <op:Op> <e:UnaryClass<Op,NextClass>> => Expr::UnOp(op, Box::new(e)),
    NextClass,
};

Type: ProtoType<'input> = {
    <SimpleType>,
    <from:SimpleType> "->" <to:Type> => ProtoType::Function(Box::new(from), Box::new(to)),
};

SimpleType: ProtoType<'input> = {
    "()" => ProtoType::Unit,
    "int" => ProtoType::Int,
    "bool" => ProtoType::Bool,
    "string" => ProtoType::String,
    "(" <Type> ")",
    <ID> => ProtoType::Generic(<>),
    <name:ID> <tp:SimpleType> => ProtoType::Sum(name, Box::new(tp)),
    "(" <Comma2<Type>> ")" => ProtoType::Tuple(<>),
    <start: @L> ! <end: @R> => { errors.push(start); ProtoType::Error(<>) },
};

pub Expr = Class<Op0,Expr1>;       // and or
Expr1 = Class<Op1,Expr2>;       // compare
Expr2 = Class<Op2,Expr3>;       // eq !=
Expr3 = Class<Op3,Expr4>;       // factor
Expr4 = Class<Op4,Expr5>;       // term
Expr5 = UnaryClass<Op5,Term>;   //unary

Term: Expr<'input>  = {
    <hd:Base> <v:Base*> => {
        let mut expr = hd;
        for e in v {
            expr = Expr::Application(Box::new(expr), Box::new(e));
        }
        expr
    },
};

Base: Expr<'input> = {
    "if" <cond:Expr> "then" <a:Expr> "else" <b:Expr> "end" => {
        Expr::Conditional(Box::new(cond), Box::new(a), Box::new(b))
    },
    "{" <v:Comma<Arm>> "}" => Expr::Closure(v),
    <Literal> => Expr::Literal(<>),
    <bound:ID> => Expr::Bound(bound),
    "(" <Comma2<Expr>> ")" => Expr::Tuple(<>),
    "(" <Expr> ")",
    <start: @L> ! <end: @R> => { errors.push(start); Expr::Error(<>) },
};

Literal: Literal<'input> = {
    r#""(\\.|[^"\\])*""# => Literal::String(&<>[1..(<>.len()-1)]),
    r"[0-9]+" => Literal::Int(isize::from_str_radix(<>, 10).unwrap()),
    "true" => Literal::Bool(true),
    "false" => Literal::Bool(false),
    "()" => Literal::Unit,
};

Op0: BinOpcode = {
    "and" => BinOpcode::And,
    "or" => BinOpcode::Or,
};

Op1: BinOpcode = {
    ">" => BinOpcode::Greater,
    "<" => BinOpcode::Less,
    ">=" => BinOpcode::GreaterEq,
    "<=" => BinOpcode::LessEq,
};

Op2: BinOpcode = {
    "=" => BinOpcode::Equal,
    "!=" => BinOpcode::NotEq,
};

Op3: BinOpcode = {
    "+" => BinOpcode::Add,
    "-" => BinOpcode::Sub,
};

Op4: BinOpcode = {
    "*" => BinOpcode::Mul,
    "/" => BinOpcode::Div,
    "%" => BinOpcode::Mod,
};

Op5: UnOpcode = {
    "not" => UnOpcode::Not,
    "-" => UnOpcode::Minus,
};

ID: &'input str = {
    r"[a-zA-Z_][a-zA-Z_'0-9]*" => <>,
}